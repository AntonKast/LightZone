# Maintainer: KITAGAWA Masahiro <arctica0316@gmail.com>
pkgname="lightzone"
pkgver=4.1.0.beta11
pkgrel=2
pkgdesc="Open-source professional-level digital darkroom software"
url="http://lightzoneproject.org/"
license=('custom:BSD-3-Clause')
arch=('i686' 'x86_64')

depends=('jre7-openjdk' 'javahelp2' 'lcms2' 'libjpeg-turbo' 'libtiff')
makedepends=('jdk7-openjdk' 'apache-ant' 'autoconf' 'gcc' 'make' 'tidyhtml' 'git' 'libx11' 'pkg-config' 'javahelp2' 'lcms2' 'libjpeg-turbo' 'libtiff')

source=("${pkgname}-${pkgver}.tar.bz2")
md5sums=()

build() {
  # Set $JAVA_HOME and $ANT_HOME
  [ -f /etc/profile.d/openjdk7.sh ] && source /etc/profile.d/openjdk7.sh
  [ -f /etc/profile.d/jdk.sh ] && source /etc/profile.d/jdk.sh
  [ -f /etc/profile.d/apache-ant.sh ] && source /etc/profile.d/apache-ant.sh

  cd "${srcdir}/${pkgname}-${pkgver}/"
  ant -f linux/build.xml jar
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  instdir="/opt/${pkgname}"
  install -dm 0755 "${pkgdir}/${instdir}"
  cp -pH lightcrafts/products/dcraw_lz "${pkgdir}/${instdir}"
  cp -pH lightcrafts/products/LightZone-forkd "${pkgdir}/${instdir}"
  cp -pH linux/products/*.so "${pkgdir}/${instdir}"
  cp -pH linux/products/*.jar "${pkgdir}/${instdir}"
  
# create icons and shortcuts
  _datadir=/usr/share
  install -dm 0755 "${pkgdir}/${_datadir}/applications"
  install -m 644 linux/products/lightzone.desktop "${pkgdir}/${_datadir}/applications/"
  cp -pHR linux/icons "${pkgdir}/${_datadir}/"

  _bindir=/usr/bin
  install -dm 0755 "${pkgdir}/${_bindir}"
  install -m 755 "linux/products/${pkgname}" "${pkgdir}/${_bindir}"
}
